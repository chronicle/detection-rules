/*
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule aws_cloudfront_insecure_ssl_policy {

  meta:
    author = "Google Cloud Security"
    description = "Detects when an insecure SSL policy is configured for an AWS CloudFront distribution."
    rule_id = "mr_dc0d66a6-121c-4b01-9370-3bf7fd25de1e"
    rule_name = "AWS CloudFront Insecure SSL Policy"
    mitre_attack_tactic = "Defense Evasion"
    mitre_attack_technique = "Weaken Encryption: Reduce Key Space"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1600/001/"
    mitre_attack_version = "v15.1"
    type = "Alert"
    data_source = "AWS CloudTrail"
    platform = "AWS"
    severity = "Low"
    priority = "Low"

  events:
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    ($cloudtrail.metadata.product_event_type = "CreateDistribution" or $cloudtrail.metadata.product_event_type = "UpdateDistribution")
    $cloudtrail.target.resource.attribute.labels["requestParameters.distributionConfig.viewerCertificate.minimumProtocolVersion"] = "TLSv1" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.distributionConfig.viewerCertificate.minimumProtocolVersion"] = "SSLv3" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.distributionConfig.viewerCertificate.minimumProtocolVersion"] = "TLSv1_2016" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.distributionConfig.viewerCertificate.minimumProtocolVersion"] = "TLSv1.1_2016"

  outcome:
    $risk_score = max(35)
    $mitre_attack_tactic = "Defense Evasion"
    $mitre_attack_technique = "Weaken Encryption: Reduce Key Space"
    $mitre_attack_technique_id = "T1600.001"
    $event_count = count_distinct($cloudtrail.metadata.id)
    $network_http_user_agent = array_distinct($cloudtrail.network.http.user_agent)
    $principal_ip = array_distinct($cloudtrail.principal.ip)
    $principal_ip_country = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principal_user_display_name = array_distinct($cloudtrail.principal.user.user_display_name)
    $recipient_aws_account_id = array_distinct($cloudtrail.additional.fields["recipientAccountId"])
    $aws_region = array_distinct($cloudtrail.principal.location.name)
    $accessed_via_console = array_distinct($cloudtrail.target.resource.attribute.labels["sessionCredentialFromConsole"])
    $cloudfront_distribution = array_distinct($cloudtrail.target.resource.attribute.labels["responseElements.distribution.aRN"])
    $ssl_security_policy = array_distinct($cloudtrail.target.resource.attribute.labels["requestParameters.distributionConfig.viewerCertificate.minimumProtocolVersion"])

  condition:
    $cloudtrail
}
