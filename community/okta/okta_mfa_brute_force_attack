rule okta_mfa_brute_force_attack {

  meta:
    author = "Chronicle Security"
    description = "Detects a successful login after multiple failed MFA pushes"
    reference = "https://sec.okta.com/pushfatigueworkflows"
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Brute Force"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1110/"
    mitre_attack_version = "v13.1"
    type = "Alert"
    data_source = "Okta"
    severity = "Medium"
    priority = "Medium"

  events:
    $push.metadata.product_name = "Okta"
    $push.metadata.vendor_name = "Okta"
    $push.metadata.event_type = "USER_UNCATEGORIZED"
    $push.metadata.product_event_type = "system.push.send_factor_verify_push"
    $push.target.user.userid = $userid

    $auth.metadata.event_type = "USER_LOGIN"
    $auth.metadata.product_event_type = "user.authentication.auth_via_mfa"
    $auth.metadata.product_name = "Okta"
    $auth.metadata.vendor_name = "Okta"
    $auth.security_result.action = "ALLOW"
    $auth.target.user.userid = $userid

    $push.metadata.event_timestamp.seconds <= $auth.metadata.event_timestamp.seconds

  match:
    $userid over 15m

  outcome:
    $mitre_attack_tactic = array_distinct("Credential Access")
    $mitre_attack_technique = array_distinct("Brute Force")
    $push_target_user_agent = array_distinct($push.network.http.user_agent)
    $push_principal_ip = array_distinct($push.principal.ip)
    $push_principal_ip_country = array_distinct($push.principal.ip_geo_artifact.location.country_or_region)
    $push_principal_ip_state = array_distinct($push.principal.ip_geo_artifact.location.state)
    $push_principal_ip_city = array_distinct($push.principal.location.city)
    $push_principal_user_email_addresses = array_distinct ($push.principal.user.email_addresses)
    $push_security_result_summary = array_distinct($push.security_result.summary)
    $push_target_user_email_addresses = array_distinct($push.target.user.email_addresses)
    $push_target_user_userid = array_distinct($push.target.user.userid)
    $auth_target_user_agent = array_distinct($auth.network.http.user_agent)
    $auth_principal_ip = array_distinct($auth.principal.ip)
    $auth_principal_ip_country = array_distinct($auth.principal.ip_geo_artifact.location.country_or_region)
    $auth_principal_ip_state = array_distinct($auth.principal.ip_geo_artifact.location.state)
    $auth_principal_ip_city = array_distinct($auth.principal.location.city)
    $auth_principal_user_email_addresses = array_distinct ($auth.principal.user.email_addresses)
    $auth_security_result_summary = array_distinct($auth.security_result.summary)
    $auth_target_user_email_addresses = array_distinct($auth.target.user.email_addresses)
    $auth_target_user_userid = array_distinct($auth.target.user.userid)

  condition:
    #push > 4 and $auth
}
