rule okta_threatinsight_suspected_brute_force_attack {

  meta:
    author = "Chronicle Security"
    description = "Okta ThreatInsight detects multiple login failures from the same IP across one or more Okta orgs."
    reference = "https://help.okta.com/en-us/Content/Topics/Security/threat-insight/configure-threatinsight-system-log.htm"
    mitre_attack_tactic = "Credential Access"
    mitre_attack_technique = "Brute Force: Password Guessing"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1110/001/"
    mitre_attack_version = "v13.1"
    type = "Alert"
    data_source = "Okta"
    severity = "Medium"
    priority = "Medium"

  events:
    $bruteforce.metadata.product_name = "Okta"
    $bruteforce.metadata.vendor_name = "Okta"
    $bruteforce.metadata.event_type = "USER_UNCATEGORIZED"
    $bruteforce.metadata.product_event_type = "security.threat.detected"
    $bruteforce.security_result.category_details = "Login Failures"
    $bruteforce.principal.user.userid = $userid

  match:
    $userid over 1h

  outcome:
    $risk_score = max(
        // If the IP Address is marked as suspicious IP address by Okta ThreatInsight
        if($bruteforce.security_result.detection_fields["threatSuspected"] = "true", 30) +
        // Unauthorized target geographies
        if($bruteforce.principal.ip_geo_artifact.location.country_or_region = "Cuba", 20) +
        if($bruteforce.principal.ip_geo_artifact.location.country_or_region = "Iran", 20) +
        if($bruteforce.principal.ip_geo_artifact.location.country_or_region = "North Korea", 20) +
        if($bruteforce.principal.ip_geo_artifact.location.country_or_region = "Russia", 20) +
        if($bruteforce.principal.ip_geo_artifact.location.country_or_region = "Syria", 20)
    )
    $mitre_attack_tactic = array_distinct("Credential Access")
    $mitre_attack_technique = array_distinct("Brute Force: Password Guessing")
    $target_user_agent = array_distinct($bruteforce.network.http.user_agent)
    $principal_ip = array_distinct($bruteforce.principal.ip)
    $principal_ip_country = array_distinct($bruteforce.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($bruteforce.principal.ip_geo_artifact.location.state)
    $principal_ip_city = array_distinct($bruteforce.principal.location.city)
    $security_result_summary = array_distinct($bruteforce.security_result.summary)

  condition:
    $bruteforce
}
