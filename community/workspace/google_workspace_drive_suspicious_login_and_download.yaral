/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule google_workspace_drive_suspicious_login_and_download {

  meta:
    author = "Google Cloud Security"
    description = "Identifies when users download documents from Google Drive after a suspicious login."
    mitre_attack_tactic = "Exfiltration"
    mitre_attack_technique = "Exfiltration Over Web Service"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1567/"
    mitre_attack_version = "v13.1"
    type = "Alert"
    data_source = "Workspace Activity"
    severity = "High"
    priority = "High"

  events:
    // Suspicious Login Event
    $login.metadata.vendor_name = "Google Workspace"
    $login.metadata.product_name = "login"
    $login.metadata.product_event_type = "login_success"
    $login.about.labels["is_suspicious"] = "true"

    // Document Download Event
    $download.metadata.vendor_name = "Google Workspace"
    $download.metadata.product_name = "drive"
    $download.metadata.product_event_type = "download"

    // Joining login and download events using userid
    $login.target.user.userid = $download.principal.user.userid

    // Placeholders for match section
    $login.principal.ip = $principal_ip
    $login.target.user.userid = $target_userid

    // First login event occurs later a download event
    $login.metadata.event_timestamp.seconds < $download.metadata.event_timestamp.seconds

  match:
    $target_userid, $principal_ip over 1h

  outcome:

    $risk_score = max(75)
    $mitre_attack_tactic = "Exfiltration"
    $mitre_attack_technique = "Exfiltration Over Web Service"
    $mitre_attack_technique_id = "T1567"
    $event_count = count_distinct($download.metadata.id)
    $product_event_type = array_distinct($download.metadata.product_event_type)
    $userid = array_distinct($download.principal.user.userid)
    $doc_type = array_distinct($download.src.resource.attribute.labels["doc_type"])
    $owner = array_distinct($download.target.resource.attribute.labels["owner"])
    $doc_name = array_distinct($download.target.resource.name)
    $doc_id = array_distinct($download.target.resource.product_object_id)

  condition:
    $login and $download
}
