/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule mitre_attack_T1021_002_windows_admin_share_with_user_entity {

  meta:
    author = "Google Cloud Security"
    description = "Net use commands for SMB/Windows admin shares focused on specific user entity characteristics"
    rule_id = "mr_18cff6cc-0d2d-4b83-9424-c854c84c4241"
    rule_name = "MITRE ATT&CK T1021.002 Windows Admin Share With User Entity"
    tactic = "TA0008"
    technique = "T1021.002"
    type = "alert"
    tags = "user entity"
    platform = "Windows"
    assumption = "Assumes ingestion of Windows users to entity graph"
    data_source = "microsoft sysmon, microsoft windows events"
    severity = "Low"
    priority = "Low"

  events:
    $process.metadata.event_type = "PROCESS_LAUNCH"
    $process.target.process.command_line = /net.*use.*(C|ADMIN|IPC)\$/ nocase
    $process.principal.user.userid = $userid

    // Correlate with user entity data
    $user.graph.entity.user.userid  = $userid
    $user.graph.metadata.entity_type = "USER"
    $user.graph.metadata.source_type = "ENTITY_CONTEXT"

    /* Following two lines are used for exercise #4 in rules workshop, comment out if you want to
    demonstrate the same logic to run based on group */
    //$user.graph.entity.user.department != "Information Technology" or
    //$user.graph.entity.user.title = "Intern"

    // Can be modified to focus rule on different groups or removed if not needed
    any $user.graph.relations.entity.group.group_display_name = "Domain Admins"

  match:
    $userid over 5m

  outcome:
    $risk_score = 35
    $event_count = count_distinct($process.metadata.id)
    // added to populate alert graph with additional context
    $principal_hostname = array_distinct($process.principal.hostname)
    $principal_process_pid = array_distinct($process.principal.process.pid)
    $principal_process_command_line = array_distinct($process.principal.process.command_line)
    $principal_process_file_sha256 = array_distinct($process.principal.process.file.sha256)
    $principal_process_file_full_path = array_distinct($process.principal.process.file.full_path)
    $principal_process_product_specific_process_id = array_distinct($process.principal.process.product_specific_process_id)
    $principal_process_parent_process_product_specific_process_id = array_distinct($process.principal.process.parent_process.product_specific_process_id)
    $target_process_pid = array_distinct($process.target.process.pid)
    $target_process_command_line = array_distinct($process.target.process.command_line)
    $target_process_file_sha256 = array_distinct($process.target.process.file.sha256)
    $target_process_file_full_path = array_distinct($process.target.process.file.full_path)
    $target_process_product_specific_process_id = array_distinct($process.target.process.product_specific_process_id)
    // Commented out principal.user.userid because it is already represented in graph as match variable. If match changes, can uncomment to add to results
    //$principal_user_userid = array_distinct($process.principal.user.userid)

  condition:
    $process and $user
}
