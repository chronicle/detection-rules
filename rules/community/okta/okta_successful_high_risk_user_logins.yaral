/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule okta_successful_high_risk_user_logins {

  meta:
    author = "Google Cloud Security"
    description = "Detects successfully authenticated user logins based on Okta's Behavior Detection pattern analysis."
    rule_id = "mr_ef123f09-f2ae-4d29-bb69-896d708f6a27"
    rule_name = "Okta Successful High Risk User Logins"
    reference = "https://help.okta.com/en-us/Content/Topics/Security/behavior-detection/logs-behavior-detection.htm"
    mitre_attack_tactic = "Defense Evasion, Persistence, Privilege Escalation, Initial Access"
    mitre_attack_technique = "Valid Accounts"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1078/"
    mitre_attack_version = "v13.1"
    type = "Hunt"
    false_positives = "This rule causes False Positives if the user is a net new user."
    data_source = "Okta"
    severity = "Medium"
    priority = "Medium"

  events:
    $login.metadata.product_name = "Okta"
    $login.metadata.vendor_name = "Okta"
    $login.metadata.event_type = "USER_LOGIN"
    $login.metadata.product_event_type = "user.session.start"
    $login.security_result.summary = "User login to Okta"
    $login.security_result.action = "ALLOW"
    $login.principal.user.userid = $userid

    // Detect only for existing users and not net new users
    $first_seen.graph.metadata.entity_type = "USER"
    $first_seen.graph.entity.user.first_seen_time.seconds < $login.metadata.event_timestamp.seconds
    $first_seen.graph.entity.user.userid = $userid

  match:
    $userid over 5m

  outcome:
    $risk_score = max(
        // Based on Okta's Behavior Detection pattern analysis
        if($login.security_result.description = /New Geo-Location=POSITIVE/, 10) +
        if($login.security_result.description = /New Device=POSITIVE/, 20) +
        if($login.security_result.description = /New IP=POSITIVE/, 5) +
        if($login.security_result.description = /New City=POSITIVE/, 5) +
        if($login.security_result.description = /New State=POSITIVE/, 10) +
        if($login.security_result.description = /New Country=POSITIVE/, 10) +
        if($login.security_result.description = /Velocity=POSITIVE/, 10) +
        // Unauthorized target geographies
        if($login.principal.ip_geo_artifact.location.country_or_region = "Cuba", 20) +
        if($login.principal.ip_geo_artifact.location.country_or_region = "Iran", 20) +
        if($login.principal.ip_geo_artifact.location.country_or_region = "North Korea", 20) +
        if($login.principal.ip_geo_artifact.location.country_or_region = "Russia", 20) +
        if($login.principal.ip_geo_artifact.location.country_or_region = "Syria", 20)
    )
    $mitre_attack_tactic = "Defense Evasion, Persistence, Privilege Escalation, Initial Access"
    $mitre_attack_technique = "Valid Accounts"
    $target_user_agent = array_distinct($login.network.http.user_agent)
    $principal_ip = array_distinct($login.principal.ip)
    $principal_ip_country = array_distinct($login.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($login.principal.ip_geo_artifact.location.state)
    $principal_ip_city = array_distinct($login.principal.location.city)
    $security_result_summary = array_distinct($login.security_result.summary)
    $principal_user_managers_email_addresses = array_distinct($login.principal.user.managers.email_addresses)
    $principal_user_userid = array_distinct($login.principal.user.userid)
    $target_user_email_addresses = array_distinct($login.target.user.email_addresses)
    $target_user_userid = array_distinct($login.target.user.userid)
    $security_result_description = array_distinct($login.security_result.description)

  condition:
    $login and $first_seen and $risk_score >= 50
}
