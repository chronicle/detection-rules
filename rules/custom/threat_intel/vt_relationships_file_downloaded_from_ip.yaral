/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule vt_relationships_file_downloaded_from_ip {

  meta:
    author = "Google Cloud Security"
    description = "Alert on downloading a known file hash from a known IP with VT Relationships."
    rule_id = "mr_1a62ba21-6f3d-40c7-9278-b1e2d639a10b"
    rule_name = "VT Relationships File Downloaded From IP"
    type = "alert"
    tags = "vt"
    data_source = "zscalar, crowdstrike"
    severity = "High"
    priority = "High"

  events:
    // NETWORK_HTTP
    $network.metadata.event_type = "NETWORK_HTTP"
    $network.principal.user.userid = $userid
    $network.target.ip = $ip

    // FILE_CREATION
    $file.metadata.event_type = "FILE_CREATION"
    $file.target.user.userid = $userid
    $file.target.file.sha256 = $file_hash

    // Network HTTP event must occur before the file creation event
    $network.metadata.event_timestamp.seconds < $file.metadata.event_timestamp.seconds

    //Uses VirusTotal integration via entity graph, this section maps to target file hash
    $vt.graph.metadata.entity_type = "FILE"
    $vt.graph.metadata.source_type = "GLOBAL_CONTEXT"
    $vt.graph.metadata.vendor_name = "VirusTotal"
    $vt.graph.metadata.product_name = "VirusTotal Relationships"
    $vt.graph.entity.file.sha256 = $file_hash

    //Uses VirusTotal integration via entity graph, this section maps the hash to the ip address it is downloaded from
    $vt.graph.relations.entity_type = "IP_ADDRESS"
    $vt.graph.relations.relationship = "DOWNLOADED_FROM"
    $vt.graph.relations.entity.ip = $ip

  match:
    $userid over 5m

  outcome:
    $risk_score = 85
    // added to populate alert graph with additional context
    $principal_hostname = array_distinct($network.principal.hostname)
    $target_hostname = array_distinct($network.target.hostname)
    $principal_process_pid = array_distinct($file.principal.process.pid)
    $principal_process_command_line = array_distinct($file.principal.process.command_line)
    $principal_process_file_sha256 = array_distinct($file.principal.process.file.sha256)
    $principal_process_file_full_path = array_distinct($file.principal.process.file.full_path)
    $principal_process_product_specific_process_id = array_distinct($file.principal.process.product_specific_process_id)
    $principal_process_parent_process_product_specific_process_id = array_distinct($file.principal.process.parent_process.product_specific_process_id)
    $target_process_pid = array_distinct($file.target.process.pid)
    $target_process_command_line = array_distinct($file.target.process.command_line)
    $target_process_file_sha256 = array_distinct($file.target.process.file.sha256)
    $target_process_file_full_path = array_distinct($file.target.process.file.full_path)
    $target_process_product_specific_process_id = array_distinct($file.target.process.product_specific_process_id)
    $target_process_parent_process_product_specific_process_id = array_distinct($file.target.process.parent_process.product_specific_process_id)
    // Commented out principal.user.userid because it is already represented in graph as match variable. If match changes, can uncomment to add to results
    //$principal_user_userid = array_distinct($file.principal.user.userid)
    $target_user_userid = array_distinct($file.target.user.userid)
    $target_url = array_distinct($network.target.url)

  condition:
    $network and $file and $vt
}
