/*
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule github_enterprise_or_organization_recovery_codes_activity {

  meta:
    author = "Google Cloud Security"
    description = "Detects when the Single Sign-On (SSO) recovery codes are accessed for a GitHub enterprise or organization. An adversary may attempt to access or steal SSO recovery codes in order to maintain access to their target's GitHub environment."
    rule_id = "mr_2bb0bc58-b799-4b8f-96c3-f1cfd7cea201"
    rule_name = "GitHub Enterprise Or Organization Recovery Codes Activity"
    assumption = "Your GitHub enterprise audit log settings are configured to log the source IP address for events. Reference: https://docs.github.com/en/organizations/keeping-your-organization-secure/managing-security-settings-for-your-organization/displaying-ip-addresses-in-the-audit-log-for-your-organization"
    type = "alert"
    severity = "Medium"
    priority = "Medium"
    platform = "GitHub"
    data_source = "github"
    reference = "https://docs.github.com/en/enterprise-cloud@latest/admin/monitoring-activity-in-your-enterprise/reviewing-audit-logs-for-your-enterprise/audit-log-events-for-your-enterprise"

  events:
    $github.metadata.vendor_name = "GITHUB" nocase
    $github.metadata.product_name = "GITHUB"
    (
        // GitHub enterprise events
        $github.metadata.product_event_type = "business.recovery_codes_downloaded" or
        $github.metadata.product_event_type = "business.recovery_codes_generated" or
        $github.metadata.product_event_type = "business.recovery_codes_printed" or
        $github.metadata.product_event_type = "business.recovery_codes_viewed" or
        // GitHub organization events
        $github.metadata.product_event_type = "org.recovery_codes_downloaded" or
        $github.metadata.product_event_type = "org.recovery_codes_generated" or
        $github.metadata.product_event_type = "org.recovery_codes_printed" or
        $github.metadata.product_event_type = "org.recovery_codes_viewed"
    )
    $github.principal.user.userid = $user_id

  match:
    $user_id over 30m

  outcome:
    $risk_score = max(65)
    $event_count = count_distinct($github.metadata.id)
    $principal_ip = array_distinct($github.principal.ip)
    $principal_user_userid = array_distinct($github.principal.user.userid)
    $principal_ip_country = array_distinct($github.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($github.principal.ip_geo_artifact.location.state)
    $principal_ip_city = array_distinct($github.principal.location.city)
    $security_result_summary = array_distinct($github.security_result.summary)

  condition:
    $github
}
