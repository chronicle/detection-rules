/*
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule aws_alb_insecure_ssl_policy {

  meta:
    author = "Google Cloud Security"
    description = "Detects when an insecure SSL policy is configured for an AWS Application Load Balancer."
    rule_id = "mr_8aace83b-f9f0-45b8-95c5-a4b0bb2cbb85"
    rule_name = "AWS Application Load Balancer Insecure SSL Policy"
    mitre_attack_tactic = "Defense Evasion"
    mitre_attack_technique = "Weaken Encryption: Reduce Key Space"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1600/001/"
    mitre_attack_version = "v15.1"
    type = "Alert"
    data_source = "AWS CloudTrail"
    platform = "AWS"
    severity = "Low"
    priority = "Low"

  events:
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    ($cloudtrail.metadata.product_event_type = "ModifyListener" or $cloudtrail.metadata.product_event_type = "CreateListener")
    $cloudtrail.target.resource.attribute.labels["requestParameters.sslPolicy"] = "ELBSecurityPolicy-2016-08" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.sslPolicy"] = "ELBSecurityPolicy-TLS13-1-1-2021-06" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.sslPolicy"] = "ELBSecurityPolicy-TLS13-1-0-2021-06" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.sslPolicy"] = "ELBSecurityPolicy-TLS13-1-1-FIPS-2023-04" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.sslPolicy"] = "ELBSecurityPolicy-TLS13-1-0-FIPS-2023-04" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.sslPolicy"] = "ELBSecurityPolicy-TLS-1-1-2017-01" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.sslPolicy"] = "ELBSecurityPolicy-FS-2018-06" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.sslPolicy"] = "ELBSecurityPolicy-2015-05" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.sslPolicy"] = "ELBSecurityPolicy-TLS-1-0-2015-04" or
    $cloudtrail.target.resource.attribute.labels["requestParameters.sslPolicy"] = "ELBSecurityPolicy-FS-1-1-2019-08"

  outcome:
    $risk_score = max(35)
    $mitre_attack_tactic = "Defense Evasion"
    $mitre_attack_technique = "Weaken Encryption: Reduce Key Space"
    $mitre_attack_technique_id = "T1600.001"
    $event_count = count_distinct($cloudtrail.metadata.id)
    $network_http_user_agent = array_distinct($cloudtrail.network.http.user_agent)
    $principal_ip = array_distinct($cloudtrail.principal.ip)
    $principal_ip_country = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principal_user_display_name = array_distinct($cloudtrail.principal.user.user_display_name)
    $recipient_aws_account_id = array_distinct($cloudtrail.additional.fields["recipientAccountId"])
    $aws_region = array_distinct($cloudtrail.principal.location.name)
    $accessed_via_console = array_distinct($cloudtrail.target.resource.attribute.labels["sessionCredentialFromConsole"])
    $target_resource_name = array_distinct($cloudtrail.target.resource.name)
    $ssl_security_policy = array_distinct($cloudtrail.target.resource.attribute.labels["requestParameters.sslPolicy"])

  condition:
    $cloudtrail
}
