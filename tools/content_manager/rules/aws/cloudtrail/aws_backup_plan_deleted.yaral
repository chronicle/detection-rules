/*
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule aws_backup_plan_deleted {

  meta:
    author = "Google Cloud Security"
    description = "Detects when AWS Backup Plan is deleted."
    rule_id = "mr_ac4b93af-f166-44fa-951d-83f08ded6fd8"
    rule_name = "AWS Backup Plan Deleted"
    mitre_attack_tactic = "Impact"
    mitre_attack_technique = "Inhibit System Recovery"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1490/"
    mitre_attack_version = "v15.1"
    type = "Alert"
    data_source = "AWS CloudTrail"
    platform = "AWS"
    severity = "Low"
    priority = "Low"

  events:
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    $cloudtrail.metadata.event_type = "RESOURCE_DELETION"
    ($cloudtrail.metadata.product_event_type = "DeleteBackupPlan" or
    $cloudtrail.metadata.product_event_type = "DeleteBackupSelection")
    $cloudtrail.security_result.action = "ALLOW"

  outcome:
    $risk_score = max(35)
    $mitre_attack_tactic = "Impact"
    $mitre_attack_technique = "Inhibit System Recovery"
    $mitre_attack_technique_id = "T1490"
    $event_count = count_distinct($cloudtrail.metadata.id)
    $network_http_user_agent = array_distinct($cloudtrail.network.http.user_agent)
    $principal_ip = array_distinct($cloudtrail.principal.ip)
    $principal_ip_country = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principal_user_display_name = array_distinct($cloudtrail.principal.user.user_display_name)
    $recipient_aws_account_id = array_distinct($cloudtrail.additional.fields["recipientAccountId"])
    $aws_region = array_distinct($cloudtrail.principal.location.name)

  condition:
    $cloudtrail
}
