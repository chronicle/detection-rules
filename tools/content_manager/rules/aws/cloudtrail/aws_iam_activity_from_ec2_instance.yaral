/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule aws_iam_activity_from_ec2_instance {

  meta:
    author = "Google Cloud Security"
    description = "Detect AWS IAM activities made by AWS EC2 instances to retain access or escalate privileges."
    rule_id = "mr_60c7bb2f-2f02-4ac5-bbe0-ba8be871f3fb"
    rule_name = "AWS IAM Activity From EC2 Instance"
    mitre_attack_tactic = "Persistence"
    mitre_attack_technique = "Account Manipulation"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1098/"
    mitre_attack_version = "v13.1"
    type = "Alert"
    data_source = "AWS CloudTrail"
    platform = "AWS"
    severity = "High"
    priority = "High"

  events:
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    $cloudtrail.target.application = "iam.amazonaws.com"
    (
    $cloudtrail.metadata.product_event_type = "CreateUser" or
    $cloudtrail.metadata.product_event_type = "AttachUserPolicy" or
    $cloudtrail.metadata.product_event_type = "CreateLoginProfile" or
    $cloudtrail.metadata.product_event_type = "UpdateLoginProfile" or
    $cloudtrail.metadata.product_event_type = "CreateAccessKey" or
    $cloudtrail.metadata.product_event_type = "CreateGroup" or
    $cloudtrail.metadata.product_event_type = "AttachGroupPolicy" or
    $cloudtrail.metadata.product_event_type = "CreateRole" or
    $cloudtrail.metadata.product_event_type = "AttachRolePolicy"
    )
    $cloudtrail.security_result.action = "ALLOW"
    $cloudtrail.principal.ip = $ip
    $cloudtrail.principal.resource.type = "AssumedRole"
    $cloudtrail.principal.user.userid = /\/i-.*$/ //regex to be improved
    re.capture($cloudtrail.principal.user.userid, ":assumed-role/.*/(.*)$") = $instanceId

  match:
    $ip over 1h

  outcome:
    $risk_score = max(35)
    $mitre_attack_tactic = "Persistence"
    $mitre_attack_technique = "Account Manipulation"
    $mitre_attack_technique_id = "T1098"
    $event_count = count_distinct($cloudtrail.metadata.id)
    $network_http_user_agent = array_distinct($cloudtrail.network.http.user_agent)
    $principal_ip = array_distinct($cloudtrail.principal.ip)
    $principal_ip_country = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principal_user_display_name = array_distinct($cloudtrail.principal.user.user_display_name)
    $recipient_aws_account_id = array_distinct($cloudtrail.additional.fields["recipientAccountId"])
    $aws_region = array_distinct($cloudtrail.principal.location.name)
    $target_resource_name = array_distinct($cloudtrail.target.resource.name)
    $target_resource_product_object_id = array_distinct($cloudtrail.target.resource.product_object_id)
    $ec2_instance_id = array_distinct($instanceId)

  condition:
    $cloudtrail
}
