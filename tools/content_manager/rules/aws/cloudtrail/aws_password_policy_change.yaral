/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule aws_password_policy_change {

  meta:
    author = "Google Cloud Security"
    description = "Detects when an existing password policy is updated or deleted in an AWS account."
    rule_id = "mr_2e66b918-acfc-46ea-b625-58732558005d"
    rule_name = "AWS Password Policy Change"
    mitre_attack_tactic = "Discovery"
    mitre_attack_technique = "Password Policy Discovery"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1201/"
    mitre_attack_version = "v13.1"
    type = "Alert"
    data_source = "AWS CloudTrail"
    platform = "AWS"
    severity = "Medium"
    priority = "Medium"

  events:
    $cloudtrail.metadata.vendor_name = "AMAZON"
    $cloudtrail.metadata.product_name = "AWS CloudTrail"
    (
     $cloudtrail.metadata.product_event_type = "UpdateAccountPasswordPolicy" or
     $cloudtrail.metadata.product_event_type = "DeleteAccountPasswordPolicy"
    )
    $cloudtrail.security_result.action = "ALLOW"

  outcome:
    $risk_score = max(50)
    $mitre_attack_tactic = "Discovery"
    $mitre_attack_technique = "Password Policy Discovery"
    $mitre_attack_technique_id = "T1201"
    $event_count = count_distinct($cloudtrail.metadata.id)
    $network_http_user_agent = $cloudtrail.network.http.user_agent
    $principal_ip = array_distinct($cloudtrail.principal.ip)
    $principal_ip_country = array_distinct($cloudtrail.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($cloudtrail.principal.ip_geo_artifact.location.state)
    $principal_user_display_name = $cloudtrail.principal.user.user_display_name
    $recipient_aws_account_id = $cloudtrail.additional.fields["recipientAccountId"]
    $aws_region = $cloudtrail.principal.location.name

  condition:
    $cloudtrail
}
