/*
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

 rule gcp_bigquery_datasets_opened_to_public {

  meta:
    author = "Google Cloud Security"
    description = "Detect when GCP BigQuery datasets are opened to public by adding allUsers or allAuthenticatedUsers to the access policy."
    rule_id = "mr_6d331411-347b-432e-ba52-ff78e4c66a3f"
    rule_name = "GCP BigQuery Datasets Opened To Public"
    mitre_attack_tactic = "Defense Evasion"
    mitre_attack_technique = "Impair Defenses"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1562/"
    mitre_attack_version = "v14.1"
    type = "Alert"
    data_source = "GCP Cloud Audit"
    platform = "GCP"
    severity = "High"
    priority = "High"

  events:
    $gcp.metadata.event_type = "USER_RESOURCE_UPDATE_PERMISSIONS"
    $gcp.metadata.log_type = "GCP_CLOUDAUDIT"
    $gcp.metadata.vendor_name = "Google Cloud Platform"
    $gcp.metadata.product_name = "BigQuery"
    $gcp.metadata.product_event_type = "google.iam.v1.IAMPolicy.SetIamPolicy"
    $gcp.target.application = "bigquery.googleapis.com"
    $gcp.security_result.action = "ALLOW"
    $gcp.target.resource.attribute.labels["dataset_change_binding_deltas_action"] = "ADD"
    $gcp.target.resource.attribute.labels["dataset_change_binding_deltas_role"] = /roles\/bigquery.*/
    $gcp.target.resource.attribute.labels["dataset_change_binding_deltas_member"] = /allUsers|allAuthenticatedUsers/
    $gcp.target.resource.resource_subtype = "bigquery_dataset"
    $gcp.target.resource.resource_type = "DATABASE"

  outcome:
    $risk_score = max(75)
    $mitre_attack_tactic = "Defense Evasion"
    $mitre_attack_technique = "Impair Defenses"
    $mitre_attack_technique_id = "T1562"
    $event_count = count_distinct($gcp.metadata.id)
    $network_http_user_agent = array_distinct($gcp.network.http.user_agent)
    $principal_ip = array_distinct($gcp.principal.ip)
    $principal_ip_country = array_distinct($gcp.principal.ip_geo_artifact.location.country_or_region)
    $principal_ip_state = array_distinct($gcp.principal.ip_geo_artifact.location.state)
    $principal_user_id = $gcp.principal.user.userid
    $principal_user_display_name = $gcp.principal.user.user_display_name
    $target_resource_name = $gcp.target.resource.name
    $event_name = $gcp.metadata.product_event_type

  condition:
    $gcp
}
