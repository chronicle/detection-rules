/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule vt_relationships_file_contacts_domain {

  meta:
    author = "Google Cloud Security"
    description = "Alert on known Hash querying a known hostname with VT Relationships"
    rule_id = "mr_42ee37ba-cb36-49f2-b784-05ca0bd55b9f"
    rule_name = "VT Relationships File Contacts Domain"
    type = "alert"
    tags = "vt"
    data_source = "microsoft sysmon, crowdstrike"
    severity = "High"
    priority = "High"

  events:
    $dns.metadata.event_type = "NETWORK_DNS"
    $dns.principal.process.file.sha256 = $file_hash
    $dns.network.dns.questions.name = $domain
    strings.coalesce($dns.principal.hostname, $dns.target.user.userid) = $hostname_or_userid

    // Uses VirusTotal integration via entity graph, this section maps to principal process file hash
    $vt.graph.metadata.entity_type = "FILE"
    $vt.graph.metadata.source_type = "GLOBAL_CONTEXT"
    $vt.graph.metadata.vendor_name = "VirusTotal"
    $vt.graph.metadata.product_name = "VirusTotal Relationships"
    $vt.graph.entity.file.sha256 = $file_hash

    // Uses VirusTotal integration via entity graph, this section maps the hash to the domain it contacts
    $vt.graph.relations.entity_type = "DOMAIN_NAME"
    $vt.graph.relations.relationship = "CONTACTS"
    $vt.graph.relations.entity.domain.name = $domain

  match:
    $hostname_or_userid over 5m

  outcome:
    $risk_score = 85
    $event_count = count_distinct($dns.metadata.id)
    $network_dns_questions_name = array_distinct($dns.network.dns.questions.name)
    $network_dns_answers_data = array_distinct($dns.network.dns.answers.data)
    // added to populate alert graph with additional context
    $principal_ip = array_distinct($dns.principal.ip)
    $target_ip = array_distinct($dns.target.ip)
    $principal_process_pid = array_distinct($dns.principal.process.pid)
    $principal_process_file_full_path = array_distinct($dns.principal.process.file.full_path)
    $principal_process_product_specific_process_id = array_distinct($dns.principal.process.product_specific_process_id)
    $principal_user_userid = array_distinct($dns.principal.user.userid)
    $principal_process_command_line = array_distinct($dns.principal.process.command_line)
    $principal_process_file_sha256 = array_distinct($dns.principal.process.file.sha256)
    $principal_process_parent_process_product_specific_process_id = array_distinct($dns.principal.process.parent_process.product_specific_process_id)

  condition:
    $dns and $vt
}
