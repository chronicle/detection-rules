/*
 * Copyright 2023 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rule google_workspace_ownership_transferred_on_google_drive {

  meta:
    author = "Google Cloud Security"
    description = "Identifies when a Google Workspace user transfers the ownership of a file."
    rule_id = "mr_05cddcc9-abf6-4c4c-9518-e2218a1f5c84"
    rule_name = "Google Workspace Ownership Transferred On Google Drive"
    mitre_attack_tactic = "Collection"
    mitre_attack_technique = "Data Staged: Remote Data Staging"
    mitre_attack_url = "https://attack.mitre.org/techniques/T1074/002/"
    mitre_attack_version = "v13.1"
    type = "Alert"
    data_source = "Workspace Activity"
    severity = "High"
    priority = "High"

  events:
    $ws.metadata.vendor_name = "Google Workspace"
    $ws.metadata.product_name = "admin"
    $ws.metadata.product_event_type = "CREATE_DATA_TRANSFER_REQUEST"
    $ws.security_result.category_details = "USER_SETTINGS"

  outcome:
    $risk_score = max(70)
    $mitre_attack_tactic = "Collection"
    $mitre_attack_technique = "Data Staged: Remote Data Staging"
    $mitre_attack_technique_id = "T1074.002"
    $event_count = count_distinct($ws.metadata.id)
    $principal_ip = array_distinct($ws.principal.ip)
    $principal_country = array_distinct($ws.principal.ip_geo_artifact.location.country_or_region)
    $principal_state = array_distinct($ws.principal.ip_geo_artifact.location.state)
    $principal_user_emails = array_distinct($ws.principal.user.email_addresses)
    $target_user_emails = array_distinct($ws.target.user.email_addresses)
    $principal_user_id = $ws.principal.user.userid
    $target_application = $ws.target.application
    $src_user_emails = array_distinct($ws.src.user.email_addresses)

  condition:
    $ws
}
